plugins {
    id 'maven-publish'
	  id 'signing'
    id 'java-library'
    id("io.github.gradle-nexus.publish-plugin") version "1.1.0"
}

group 'net.wushilin'
archivesBaseName = "JDBCLeakDetector"
version '1.0.0'

repositories {
    mavenCentral()
}

java {
    sourceCompatibility = "1.7"
    targetCompatibility = "1.7"
}


dependencies {
    testImplementation 'com.mysql:mysql-connector-j:8.0.32'
}

test {
    useJUnitPlatform()
}

java {
    withJavadocJar()
    withSourcesJar()
}

nexusPublishing {
    repositories {
        sonatype {
            //stagingProfileId = sonatypeStagingProfileId
            username = "${System.env.ossrhUsername}"
            password = "${System.env.ossrhPassword}"
	    nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'jdbcleakdetector'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Detect your unclosed JDBC resources'
                description = 'A library to detect where you forgot to close your JDBC resources'
                url = 'https://github.com/wushilin/JDBCLeakDetector'
                properties = [
                    myProp: "value",
                    "prop.with.dots": "anotherValue"
                ]
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'wushilin'
                        name = 'Wu Shilin'
                        email = 'wushilin.sg@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/wushilin/JDBCLeakDetector.git'
                    developerConnection = 'scm:git:git://github.com/wushilin/JDBCLeakDetector.git'
                    url = 'https://github.com/wushilin/JDBCLeakDetector'
                }
            }
        }
    }
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}


artifacts {
    archives javadocJar, sourcesJar
}


signing {
    sign publishing.publications.mavenJava
}

test {
    useJUnitPlatform()
}
